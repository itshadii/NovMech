
Option Explicit

Sub main()

Dim swApp           As SldWorks.SldWorks
Dim swModel         As SldWorks.ModelDoc2
Dim swDrawModel     As SldWorks.ModelDoc2
Dim swDraw          As SldWorks.DrawingDoc
Dim swView          As SldWorks.View
Dim nErrors         As Long
Dim nWarnings       As Long
Dim Revision        As String
Dim Inflo           As String
'Dim dFileName       As String
Dim pFileName       As String
Dim Description     As String
Dim folderPath      As String
Dim APath           As String 'Archived folder
Dim Path            As String
Dim StartTime       As Double
Dim EndTime         As Double
Dim ElapsedTime     As Double
Dim FSO             As Object
Dim R               As Integer
Dim SourceFileName  As String, DestinFileName As String


StartTime = Timer
'----------------------------------------------------------------------'
' Sets instance to allow SolidWorks to manipulate existing files
' using Windows commands
'----------------------------------------------------------------------'
Set FSO = CreateObject("Scripting.Filesystemobject")
'----------------------------------------------------------------------'
' Change the file to your preference here
'----------------------------------------------------------------------'
Path = "S:\TF - Transfer\Hadi Al-Saudi\07 - Test" 'Change Path here
'----------------------------------------------------------------------'
' Saves active file in an object
'----------------------------------------------------------------------'
Set swApp = Application.SldWorks
Set swDrawModel = swApp.ActiveDoc
'----------------------------------------------------------------------'
' Check to see if a drawing is loaded.
'----------------------------------------------------------------------'
If swDrawModel Is Nothing Then
        MsgBox "There is no active drawing document"
        Exit Sub
End If
'----------------------------------------------------------------------'
' Check to see if the file is a drawing.
'----------------------------------------------------------------------'
If swDrawModel.GetType <> swDocDRAWING Then
        MsgBox "Open a drawing first and then TRY again!"
        Exit Sub
End If
'----------------------------------------------------------------------'
' Check to see if the drawing is saved.
'----------------------------------------------------------------------'
If swDrawModel.GetPathName = "" Then
        MsgBox "Plese Save the Drawing and then TRY again!"
        swDrawModel.Save
        Exit Sub
End If
'----------------------------------------------------------------------'
' Gets information of the drawing and model and stores it in variables.
'----------------------------------------------------------------------'
Set swDraw = swDrawModel
Set swView = swDraw.GetFirstView
Set swView = swView.GetNextView
'----------------------------------------------------------------------'
' Determine if there is any model view
'----------------------------------------------------------------------'
If swView Is Nothing Then
        MsgBox "No View(s) found, Insert a View first and then TRY again!"
        Exit Sub
End If
'----------------------------------------------------------------------'
' Determine if there is any model view in the drawing open
'----------------------------------------------------------------------'
If swView.GetReferencedModelName = "" Then
        MsgBox "No Model View(s) found, Insert a View first and then TRY again!"
        Exit Sub
End If
'----------------------------------------------------------------------'
' Gets the Revision from Custom Properties, if the revision is empty
' or not a number it prompts an error
'----------------------------------------------------------------------'
Revision = swDraw.GetCustomInfoValue("", "Revision")
If Revision = "" Or IsNumeric(Revision) Then
Else
        MsgBox "The revision is incorrect."
        Exit Sub
End If
'----------------------------------------------------------------------'
' Gets the inflo from Custom Properties, change "inflo" to the custom property you have
'----------------------------------------------------------------------'
Inflo = swDraw.GetCustomInfoValue("", "inflo")
If Inflo = "" Then
        MsgBox "Enter Stock Code then Try Again to the Model"
        Exit Sub
End If
'----------------------------------------------------------------------'
' Gets the inflo from Custom Properties, change Description to the custom property you have
'----------------------------------------------------------------------'
Description = swDraw.GetCustomInfoValue("", "Description")
If Description = "" Then
        MsgBox "Enter Description then Try Again to the Model"
        Exit Sub
End If
'----------------------------------------------------------------------'
'Model File Name Without Extension
'----------------------------------------------------------------------'
Set swModel = swView.ReferencedDocument
pFileName = Inflo + "_" + Description  'formats the drawing/folder name
'----------------------------------------------------------------------'
' Create folder with name similar to pFileName
'----------------------------------------------------------------------'
folderPath = Path & "\" & pFileName
If Dir(folderPath, vbDirectory) = "" Then
    MkDir folderPath
    MsgBox "Folder is created in the following directory: " + folderPath
End If
'----------------------------------------------------------------------'
' Create an Archive folder, APath
'----------------------------------------------------------------------'
APath = folderPath & "\" & "Achived"
If Dir(APath, vbDirectory) = "" Then
    MkDir APath
    MsgBox "Folder is created in the following directory: " + APath
End If
'----------------------------------------------------------------------'
'Save as PDF, sets where the PDF and STEP file gets saved.
'After the \ is the format of the name of the PDF saved.
'After pdf prefix is fields required by the Save as command.
'----------------------------------------------------------------------'
swDraw.SaveAs3 folderPath & "\" & "SW_EN_DWG_" & pFileName & "_" & "R" & Revision & ".PDF", 0, 0
'----------------------------------------------------------------------'
'Save as STEP
'After the \ is the format of the name of the step file saved.
'After step prefix is fields required by the Save as command.
'----------------------------------------------------------------------'
swModel.Extension.SaveAs folderPath & "\" & "SW_EN_DWG_" & pFileName & "_" & "R" & Revision & ".step", swSaveAsCurrentVersion, swSaveAsOptions_Silent, Nothing, nErrors, nWarnings
'----------------------------------------------------------------------'
' Move Previous Revision To Arhive
' If the previous revision already exist in the archived folder
' A prompt will show up showing that the previous revision already exist.
'----------------------------------------------------------------------'
R = CInt(Revision)
If Dir(folderPath & "\" & "SW_EN_DWG_" & pFileName & "_" & "R" & R & ".PDF", vbNormal) <> "" Then
    SourceFileName = folderPath & "\" & "SW_EN_DWG_" & pFileName & "_" & "R" & R & ".PDF"
    DestinFileName = APath & "\" & "SW_EN_DWG_" & pFileName & "_" & "R" & R & ".PDF"
    If Dir(DestinFileName, vbNormal) <> "" Then
        MsgBox "Previous revision already exist in Archived folder."
        Exit Sub
    End If
    FSO.MoveFile Source:=SourceFileName, Destination:=DestinFileName
End If

If Dir(folderPath & "\" & "SW_EN_DWG_" & pFileName & "_" & "R" & R & ".Step", vbNormal) <> "" Then
    SourceFileName = folderPath & "\" & "SW_EN_DWG_" & pFileName & "_" & "R" & R & ".Step"
    DestinFileName = APath & "\" & "SW_EN_DWG_" & pFileName & "_" & "R" & R & ".Step"
    If Dir(DestinFileName, vbNormal) <> "" Then
        Exit Sub
    End If
    FSO.MoveFile Source:=SourceFileName, Destination:=DestinFileName
End If
'----------------------------------------------------------------------'
' Timer Crap
'----------------------------------------------------------------------'
EndTime = Timer
ElapsedTime = EndTime - StartTime
MsgBox "Export Complete, This task took " & ElapsedTime & " seconds to complete." & vbCr & "Files saved here: " & vbCr & folderPath

End Sub
